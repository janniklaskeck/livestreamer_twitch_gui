plugins {
    id "java"
    id "distribution"
    id "eclipse"
    id "org.sonarqube" version "2.6.2"
    id "edu.sc.seis.launch4j" version "2.4.4"
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "com.github.hierynomus.license" version "0.14.0"
}

group = "app.lsgui"
loadVersion()

description = "$project.name"
sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
    jcenter()
}

ext {
    mainClass = "app.lsgui.LsGUI"

    // Versionumbers
    pircbotVersion = "1.5.0"
    richtextfxVersion = "0.8.2"
    fontawesomefxVersion = "8.9"
    controlsfxVersion = "8.40.14"
    gsonVersion = "2.8.2"
    httpclientVersion = "9.4.0.v20161208"
    slf4jVersion = "1.8.0-beta2"
    log4jVersion = "2.11.0"
    junitVersion = "4.12"
    semVerVersion = "0.9.0"
}

dependencies {
    compile "pircbot:pircbot:$pircbotVersion"
    compile "org.fxmisc.richtext:richtextfx:$richtextfxVersion"
    compile "de.jensd:fontawesomefx:$fontawesomefxVersion"
    compile "org.controlsfx:controlsfx:$controlsfxVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-jul:$log4jVersion"
    compile "com.github.zafarkhaja:java-semver:$semVerVersion"
    compile "org.eclipse.jetty:jetty-client:$httpclientVersion"
    testCompile "junit:junit:$junitVersion"
}

sonarqube  {
    properties  {
        property "sonar.sourceEncoding", "UTF-8"
    }
}

jar {
    manifest {
        attributes "Implementation-Title": "LivestreamerGUI",
          "Implementation-Version": version,
          "Main-Class": "$mainClass"
    }
    appendix = "nolib"
}

shadowJar {
	classifier = null
    archiveName = "${project.name}-${version}.jar"
}

distributions {
    main {
        contents {
            from shadowJar
        }
    }
}

eclipse {
    project {
        natures 'org.springsource.ide.eclipse.gradle.core.nature', 'org.eclipse.jdt.core.javanature'
        buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
    }
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

launch4j {
    mainClassName = "${mainClass}"
    icon = "${buildDir}/resources/main/icon.ico"
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    outfile = "LivestreamerGUI.exe"
    supportUrl = "https://github.com/westerwave/livestreamer_twitch_gui"
    copyright = "MIT LICENSE"
    initialHeapSize = 64
    maxHeapSize = 512
}

license {
    header file("LICENSE.md")
    ignoreFailures=true
}

def loadVersion() {
    Properties props = new Properties()
    props.load(new FileInputStream("$project.rootDir/src/main/resources/version.properties"))
    def versionNumber = props.getProperty("versionNumber")
    version = versionNumber
}

