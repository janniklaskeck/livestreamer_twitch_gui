plugins {
    id "java"
    id "distribution"
    id "eclipse"
    id "org.sonarqube" version "2.1-rc2"
    id 'edu.sc.seis.launch4j' version '1.6.2'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group = 'app.lsgui'
loadVersion()

description = "$project.name"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
  mainClass = "app.lsgui.LsGUI"

  // Versionumbers
  pircbotxVersion = "2.1"
  richtextfxVersion = "0.6.10"
  fontawesomefxVersion = "8.9"
  controlsfxVersion = "8.40.11"
  gsonVersion = "2.7"
  httpclientVersion = "4.5.2"
  slf4jVersion = "1.7.21"
  log4jVersion = "2.6.2"
  junitVersion = "4.12"
}

dependencies {
    compile files(System.getProperty("java.home") + "/lib/ext/jfxrt.jar" )
    compile "org.fxmisc.richtext:richtextfx:$richtextfxVersion"
    compile "de.jensd:fontawesomefx:$fontawesomefxVersion"
    compile "org.controlsfx:controlsfx:$controlsfxVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "org.apache.httpcomponents:httpclient:$httpclientVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-jul:$log4jVersion"
    testCompile "junit:junit:$junitVersion"
}

jar {
    manifest {
        attributes "Implementation-Title": "LivestreamerGUI FatJar",
          "Implementation-Version": version,
          "Main-Class": "$mainClass"
    }
    appendix = "nolib"
}

distributions {
  main {
    contents {
      from shadowJar
    }
  }
}

eclipse {
  project {
    natures 'org.springsource.ide.eclipse.gradle.core.nature', 'org.eclipse.jdt.core.javanature'
    buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
  }
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

launch4j {
    outputDir = "$buildDir/distributions"
    mainClassName = "$mainClass"
    icon = "$buildDir/resources/main/icon.ico"
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    supportUrl = "https://github.com/westerwave/livestreamer_twitch_gui"
    copyright = "MIT LICENSE"
    language = "ENGLISH_US"
    initialHeapSize = 128
    maxHeapSize = 384
}

def loadVersion() {
    Properties props = new Properties()
    props.load(new FileInputStream("$project.rootDir/src/main/resources/version.properties"))
    def versionNumber = props.getProperty("versionNumber")
    version = versionNumber
}

